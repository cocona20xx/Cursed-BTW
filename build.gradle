
plugins {
    id 'maven-publish'
    id 'org.quiltmc.loom' version '1.2.+'
}

loom {
    setIntermediaryUrl('https://maven.legacyfabric.net/net/legacyfabric/intermediary/%1$s/intermediary-%1$s-v2.jar')
    if (System.getProperty("os.name").toLowerCase().contains("mac")) {
        runConfigs.named("client").get().getVmArgs().remove("-XstartOnFirstThread")
    }
    runConfigs.named("client").get().getVmArgs().add("-Xverify:none")
    runConfigs.named("server").get().getVmArgs().add("-Xverify:none")

}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name = 'legacy-fabric'
        url = 'https://maven.legacyfabric.net'
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        name 'HalfOf2'
        url 'https://storage.googleapis.com/devan-maven/'
    }
    maven {
        url = 'https://raw.githubusercontent.com/Devan-Kerman/Devan-Repo/master/'
    }

}

sourceSets {
    btw {
        java {
            srcDirs = ['src/btw/java']
        }
        resources {
            srcDirs = ['src/btw/resources']
        }
    }
}

def lwjglVersion = System.getProperty("os.name").toLowerCase().contains("mac") ? "2.9.1" : "2.9.0"

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    implementation 'org.apache.logging.log4j:log4j-core:2.17.1'
    implementation 'org.apache.logging.log4j:log4j-api:2.17.1'
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    implementation "org.lwjgl.lwjgl:lwjgl_util:${lwjglVersion}"
    implementation "org.lwjgl.lwjgl:lwjgl:${lwjglVersion}"
    implementation "org.lwjgl.lwjgl:lwjgl-platform:${lwjglVersion}"


    implementation 'it.unimi.dsi:fastutil:8.5.12'
    modImplementation(group: 'net.devtech', name: 'grossfabrichacks', version: '4.5') {
        exclude group: 'net.fabricmc'
    }
    implementation 'com.google.code.gson:gson:2.10.1'
    include 'com.google.code.gson:gson:2.10.1'

    include 'it.unimi.dsi:fastutil:8.5.12'
    include 'net.devtech:grossfabrichacks:4.5'

    implementation fileTree(dir: "libs", include: "**.zip")
    compileOnly fileTree(dir: "$projectDir/BTW_dev", include: "*.zip")
    compileOnly fileTree(dir: "$buildDir/BTW_dev", include: "**.jar")

    compileOnly fileTree(dir: "$buildDir/dev_run", include: "dev.jar")

    mappings fileTree(dir: "custom_mappings", include: "**.zip")
    modImplementation("org.quiltmc:quilt-loader:${project.quilt_loader_version}"
    ) {
        transitive false
    }
}

configurations {
    btwCompileClasspath.extendsFrom implementation, modImplementation
}


configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            substitute module('org.lwjgl.lwjgl:lwjgl_util:2.9.1-nightly-20130708-debug3') using module("org.lwjgl.lwjgl:lwjgl_util:${lwjglVersion}")
            substitute module('org.lwjgl.lwjgl:lwjgl:2.9.1-nightly-20130708-debug3') using module("org.lwjgl.lwjgl:lwjgl:${lwjglVersion}")
        }
        force "org.lwjgl.lwjgl:lwjgl-platform:${lwjglVersion}"
    }
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) it.options.release = 8
}

java {
    withSourcesJar()
}

compileJava {
    dependsOn('btwJar')
}

jar {
    from sourceSets.main.output.resourcesDir
    from sourceSets.main.output.classesDirs
    from fileTree("$projectDir/extras/")
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

task btwJar(type:Jar) {
    from fileTree("$projectDir/BTW_dev/")
    from sourceSets.btw.output.resourcesDir
    from sourceSets.btw.output.classesDirs
    destinationDirectory = file("$buildDir/BTW_dev")
    archiveFileName = "BTW_dev.jar"
}
